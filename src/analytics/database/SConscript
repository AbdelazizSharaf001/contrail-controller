# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

import sys

Import('AnalyticsEnv')
DatabaseEnv = AnalyticsEnv.Clone()

setup_sources = [
           'setup.py',
	   'MANIFEST.in',
          ]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(DatabaseEnv.Install("", "#/controller/src/analytics/database/" + file))

local_sources = [
           '__init__.py',
]
local_sources_rules = []
for file in local_sources:
    local_sources_rules.append(DatabaseEnv.Install("database", "#/controller/src/analytics/database/" + file))

DatabaseSandeshGenFiles = DatabaseEnv.SandeshGenPy('#controller/src/analytics/viz.sandesh')
DatabaseSandeshGenFiles += DatabaseEnv.SandeshGenPy('#controller/src/analytics/database/database_cpuinfo.sandesh') 

viz_sandesh_gen_py_dirs = [ 'viz', 'database_cpuinfo']
for dir in viz_sandesh_gen_py_dirs:
    Dir(DatabaseEnv['TOP'] + '/analytics/database/gen_py/' + dir)
viz_sandesh_gen_py_files = viz_sandesh_gen_py_dirs + [ '__init__.py' ]
viz_sandesh_gen_py_srcs = ['gen_py/' + src for src in viz_sandesh_gen_py_files]
viz_sandesh_gen_py_rules = []
for file in viz_sandesh_gen_py_srcs:
    viz_sandesh_gen_py_rules.append(DatabaseEnv.Install('database/sandesh', DatabaseEnv['TOP'] + '/analytics/database/' + file))
DatabaseEnv.Depends(viz_sandesh_gen_py_rules, DatabaseSandeshGenFiles)

cpu_info_gen_py_files = DatabaseEnv.SandeshGenPy('#/controller/src/base/sandesh/cpuinfo.sandesh')
Dir(DatabaseEnv['TOP'] + 'analytics/database/gen_py/cpuinfo')
cpu_info_gen_py_srcs = ['gen_py/cpuinfo']
cpu_info_gen_py_rules = []
for file in cpu_info_gen_py_srcs:
    cpu_info_gen_py_rules.append(DatabaseEnv.Install('database/sandesh/database_cpuinfo', DatabaseEnv['TOP'] + '/analytics/database/' + file))
DatabaseEnv.Depends(cpu_info_gen_py_rules, cpu_info_gen_py_files)

sdist_depends = [setup_sources_rules, local_sources_rules,viz_sandesh_gen_py_rules, DatabaseSandeshGenFiles]
sdist_gen = DatabaseEnv.Command('dist', 'setup.py', 'python setup.py sdist', chdir=1)
DatabaseEnv.Depends(sdist_gen, sdist_depends)
DatabaseEnv.Default(sdist_gen)

DatabaseEnv.Alias("database:node_mgr", sdist_gen)
DatabaseEnv.Alias("src/analytics/database:database", sdist_gen)

DatabaseEnv['DATABASE_PKG'] = sdist_gen
